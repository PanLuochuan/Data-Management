The following is my code for completing this assignment using hadoop pig

-- What are the optimal times of day, days of the week, and times of the year for minimizing flight delays?

rowdata = LOAD '/user/maria_dev/assignment2/2006.csv' USING PigStorage(',') AS (
    Year:int, Month:int, DayofMonth:int, DayofWeek:int, DepTime:int, CRSDepTime:int, 
    ArrTime:int, CRSArrTime:int, UniqueCarrier:chararray, FlightNum:int, TailNum:chararray, 
    ActualElapsedTime:int, CRSELapsedTime:int, AirTime:int, ArrDelay:int, DepDelay:int, 
    Origin:chararray, Dest:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, 
    CancellationCode:chararray, Diverted:int, CarrierDelay:int, WeatherDelay:int, 
    NASDelay:int, SecurityDelay:int, LateAircraftDelay:int
);
delay = FILTER rowdata BY Cancelled == 0;

--day
hours_delay = FOREACH delay GENERATE (ArrDelay + DepDelay)/2 AS AvgDelay, (int)FLOOR(CRSDepTime / 100) AS DepHour;
groups = GROUP hours_delay BY DepHour;
hour_delay = FOREACH groups GENERATE group AS DepHour,AVG(hours_delay.AvgDelay) AS MeanDelay;

DUMP hour_delay;

--week
week = FOREACH delay GENERATE 
    (ArrDelay + DepDelay)/2 AS AvgDelay,
    DayofWeek AS Week,  
    (CASE DayofWeek
        WHEN 1 THEN 'Monday'
        WHEN 2 THEN 'Tuesday'
        WHEN 3 THEN 'Wednesday'
        WHEN 4 THEN 'Thursday'
        WHEN 5 THEN 'Friday'
        WHEN 6 THEN 'Saturday'
        WHEN 7 THEN 'Sunday'
    END) AS WeekName;

weekgroup = GROUP week BY (Week, WeekName); 
week_delay = FOREACH weekgroup GENERATE
    group.Week AS Week,
    group.WeekName AS WeekName,
    AVG(week.AvgDelay) AS MeanDelay;  

order_week_delay = ORDER week_delay BY Week;  

DUMP order_week_delay;

--month
month = FOREACH delay GENERATE 
    (ArrDelay + DepDelay)/2 AS AvgDelay,
    Month,  
    (CASE Month
        WHEN 1 THEN 'Jan'
        WHEN 2 THEN 'Feb'
        WHEN 3 THEN 'Mar'
        WHEN 4 THEN 'Apr'
        WHEN 5 THEN 'May'
        WHEN 6 THEN 'Jun'
        WHEN 7 THEN 'Jul'
        WHEN 8 THEN 'Aug'
        WHEN 9 THEN 'Sept'
        WHEN 10 THEN 'Oct'
        WHEN 11 THEN 'Nov'
        WHEN 12 THEN 'Dec'
    END) AS MonthName;

monthgroup = GROUP month BY (Month, MonthName);  
month_delay = FOREACH monthgroup GENERATE 
    group.Month AS Month, 
    group.MonthName AS MonthName,
    AVG(month.AvgDelay) AS MeanDelay;

order_month_delay = ORDER month_delay BY Month;  

DUMP order_month_delay;

STORE hour_delay INTO '/user/maria_dev/assignment2/Hour_dealy' USING PigStorage(',');
STORE order_week_delay INTO '/user/maria_dev/assignment2/Week_delay' USING PigStorage(',');
STORE order_month_delay INTO '/user/maria_dev/assignment2/Month_delay' USING PigStorage(',');

-- What are the primary factors contributing to flight delays?

rowdata = LOAD '/user/maria_dev/assignment2/2006.csv' USING PigStorage(',') AS (
    Year:int, Month:int, DayofMonth:int, DayofWeek:int, DepTime:int, CRSDepTime:int, 
    ArrTime:int, CRSArrTime:int, UniqueCarrier:chararray, FlightNum:int, TailNum:chararray, 
    ActualElapsedTime:int, CRSELapsedTime:int, AirTime:int, ArrDelay:int, DepDelay:int, 
    Origin:chararray, Dest:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, 
    CancellationCode:chararray, Diverted:int, CarrierDelay:int, WeatherDelay:int, 
    NASDelay:int, SecurityDelay:int, LateAircraftDelay:int
);
delay = FILTER rowdata BY Cancelled == 0;

carrier_delay = FOREACH delay GENERATE 'CarrierDelay' AS DelayType, CarrierDelay AS DelayMinutes;
weather_delay = FOREACH delay GENERATE 'WeatherDelay' AS DelayType, WeatherDelay AS DelayMinutes;
nas_delay = FOREACH delay GENERATE 'NASDelay' AS DelayType, NASDelay AS DelayMinutes;
late_aircraft_delay = FOREACH delay GENERATE 'LateAircraftDelay' AS DelayType, LateAircraftDelay AS DelayMinutes;

combine_delay = UNION carrier_delay, weather_delay, nas_delay, late_aircraft_delay;
filter_delay = FILTER combine_delay BY DelayMinutes > 0;
group_delay = GROUP filter_delay BY DelayType;
delay_factor = FOREACH group_delay GENERATE group AS DelayType, COUNT(filter_delay) AS Frequency;

STORE delay_factor INTO '/user/maria_dev/assignment2/delay_factor' USING PigStorage(',');
DUMP delay_factor;



-- What factors predominantly lead to flight cancellations?

rowdata = LOAD '/user/maria_dev/assignment2/2006.csv' USING PigStorage(',') AS (
    Year:int, Month:int, DayofMonth:int, DayofWeek:int, DepTime:int, CRSDepTime:int, 
    ArrTime:int, CRSArrTime:int, UniqueCarrier:chararray, FlightNum:int, TailNum:chararray, 
    ActualElapsedTime:int, CRSELapsedTime:int, AirTime:int, ArrDelay:int, DepDelay:int, 
    Origin:chararray, Dest:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, 
    CancellationCode:chararray, Diverted:int, CarrierDelay:int, WeatherDelay:int, 
    NASDelay:int, SecurityDelay:int, LateAircraftDelay:int
);
cancell = FILTER rowdata BY Cancelled == 1;
group_cancell = GROUP cancell BY CancellationCode;
cancell_factor = FOREACH group_cancell GENERATE group AS CancellationCode,COUNT(cancell) AS Frequency;
STORE cancell_factor INTO '/user/maria_dev/assignment2/cancell_factors' USING PigStorage(',');
DUMP cancell_factor;

-- Which flight experiences the most frequent and significant delays and cancellations?

rowdata = LOAD '/user/maria_dev/assignment2/2006.csv' USING PigStorage(',') AS (
    Year:int, Month:int, DayofMonth:int, DayofWeek:int, DepTime:int, CRSDepTime:int, 
    ArrTime:int, CRSArrTime:int, UniqueCarrier:chararray, FlightNum:int, TailNum:chararray, 
    ActualElapsedTime:int, CRSELapsedTime:int, AirTime:int, ArrDelay:int, DepDelay:int, 
    Origin:chararray, Dest:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, 
    CancellationCode:chararray, Diverted:int, CarrierDelay:int, WeatherDelay:int, 
    NASDelay:int, SecurityDelay:int, LateAircraftDelay:int);
    
group_flight = GROUP rowdata BY (UniqueCarrier, FlightNum);

flight = FOREACH group_flight GENERATE group.UniqueCarrier AS UniqueCarrier,
    group.FlightNum AS FlightNum,SUM(rowdata.Cancelled) AS Cancellations,
    COUNT(rowdata) AS TotalFlights;
cancell = FOREACH flight GENERATE UniqueCarrier,
    FlightNum,Cancellations,TotalFlights,
    (double)Cancellations / (double)TotalFlights AS CancellationRate;
filter_stats = FILTER cancell BY TotalFlights > 30;
    
cancell_rate = FOREACH flight GENERATE UniqueCarrier,FlightNum,Cancellations,TotalFlights,
    (double)Cancellations / (double)TotalFlights AS CancellationRate;
top_cancell = ORDER filter_stats BY CancellationRate DESC;
top_cancell10 = LIMIT top_cancell 10;

DUMP top_cancell10;
STORE top_cancell10 INTO '/user/maria_dev/assignment2/top_cancell10' USING PigStorage(',');

rowdata = LOAD '/user/maria_dev/assignment2/2006.csv' USING PigStorage(',') AS (
    Year:int, Month:int, DayofMonth:int, DayofWeek:int, DepTime:int, CRSDepTime:int, 
    ArrTime:int, CRSArrTime:int, UniqueCarrier:chararray, FlightNum:int, TailNum:chararray, 
    ActualElapsedTime:int, CRSELapsedTime:int, AirTime:int, ArrDelay:int, DepDelay:int, 
    Origin:chararray, Dest:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, 
    CancellationCode:chararray, Diverted:int, CarrierDelay:int, WeatherDelay:int, 
    NASDelay:int, SecurityDelay:int, LateAircraftDelay:int
);
delay = FILTER rowdata BY Cancelled == 0;
group_data = GROUP delay BY (UniqueCarrier, FlightNum);
summary_data = FOREACH group_data {
    temp_delays = FOREACH delay GENERATE (ArrDelay + DepDelay) / 2 AS IndividualDelay;
    GENERATE
        group.UniqueCarrier AS UniqueCarrier,
        group.FlightNum AS FlightNum,
        COUNT(delay) AS TotalFlights,
        AVG(temp_delays.IndividualDelay) AS AvgArrDelay;
}; 
top_delay = ORDER summary_data BY TotalFlights DESC;
top_10_delay = LIMIT top_delay 10;
STORE top_10_delay INTO '/user/maria_dev/assignment2/top_delay' USING PigStorage(',');
DUMP top_10_delay;




